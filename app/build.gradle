import java.text.SimpleDateFormat

plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-parcelize"
}

def keyAliasExt = "leavesCZY"
def keyPasswordExt = "123456"
def storePasswordExt = "123456"

android {
    namespace "github.leavesczy.matisse.samples"
    compileSdk 33
    defaultConfig {
        applicationId "github.leavesczy.matisse.samples"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    signingConfigs {
        releaseConfig {
            storeFile file(rootDir.absolutePath + File.separator + "key.jks")
            keyAlias keyAliasExt.toString()
            storePassword storePasswordExt.toString()
            keyPassword keyPasswordExt.toString()
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.releaseConfig
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        release {
            signingConfig signingConfigs.releaseConfig
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def simpleDateFormat = new SimpleDateFormat("yyyy_MM_dd_HH_mm_ss")
            simpleDateFormat.timeZone = TimeZone.getTimeZone("Asia/Shanghai")
            def time = simpleDateFormat.format(Calendar.getInstance().time)
            output.outputFileName = "matisse_${time}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += [
                "-Xopt-in=androidx.compose.material3.ExperimentalMaterial3Api",
                "-Xopt-in=androidx.compose.foundation.ExperimentalFoundationApi",
                "-Xopt-in=androidx.compose.foundation.layout.ExperimentalLayoutApi",
        ]
    }
    packagingOptions {
        resources {
            excludes += "META-INF/*.md"
            excludes += "META-INF/*.version"
            excludes += "META-INF/*.properties"
            excludes += "META-INF/CHANGES"
            excludes += "META-INF/{AL2.0,LGPL2.1}"
            excludes += "DebugProbesKt.bin"
            excludes += "kotlin-tooling-metadata.json"
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.4.4"
    }
}

dependencies {
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation "androidx.activity:activity-compose:1.7.2"
    def composeBom = platform("androidx.compose:compose-bom:2023.06.00")
    implementation composeBom
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material3:material3"
    def coilVersion = "2.4.0"
    implementation "io.coil-kt:coil-gif:$coilVersion"
    implementation "io.coil-kt:coil-video:$coilVersion"
    implementation "io.coil-kt:coil-compose:$coilVersion"
    def glideComposeVersion = "1.0.0-alpha.3"
    implementation "com.github.bumptech.glide:compose:$glideComposeVersion"
    implementation project(path: ":matisse")
}